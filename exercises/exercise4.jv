transform CelsiusToFahrenheit {
    from temperaturCelsius oftype decimal;
    to temperaturFahrenheit oftype decimal;

    temperaturFahrenheit: (temperaturCelsius * 9) / 5 + 32 ;
}

valuetype HigherNull oftype integer {
    constraints: [ HigherNull ];
}

constraint HigherNull oftype RangeConstraint {
    lowerBound: 0;
    upperBound: 1000000000;      
}

pipeline TemperaturPipeline {
    block DataExtractor oftype HttpExtractor { url: "https://www.mowesta.com/data/measure/mowesta-dataset-20221107.zip"; }

    block ZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block TemperaturFilePicker oftype FilePicker {
        path: "/data.csv";
    }

    block TemperaturTextFileInterpreter oftype TextFileInterpreter {}
    
    block TemperaturCSVInterpreter oftype CSVInterpreter { 
        delimiter: ";";
    }

    block ColumnDataSelector oftype CellRangeSelector {
        select: range A1:K*;
    }

    block TemperaturHeaderWriter oftype CellWriter {
        at: cell E1;
        write: ["Temperatur"];
    }

    block BatterietemperaturHeaderWriter oftype CellWriter {
        at: cell J1;
        write: ["Batterietemperatur"];
    }

    block BatteryTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "Geraet" oftype HigherNull,
            "Hersteller" oftype text,
            "Model" oftype text,
            "Monat" oftype integer,
            "Temperatur" oftype decimal,
            "Batterietemperatur" oftype decimal,
            "Geraet aktiv" oftype text
        ];
    }

    block CelsiusToFahrenheitTransformer1 oftype TableTransformer {
        inputColumn: 'Temperatur';
        outputColumn: 'Temperatur';
        use: CelsiusToFahrenheit;
    }

    block CelsiusToFahrenheitTransformer2 oftype TableTransformer {
        inputColumn: 'Batterietemperatur';
        outputColumn: 'Batterietemperatur';
        use: CelsiusToFahrenheit;
    }

    block DatabaseLoader oftype SQLiteLoader { 
        table: "temperatures"; 
        file: "temperatures.sqlite"; 
    }


    DataExtractor
    ->ZipArchiveInterpreter
    ->TemperaturFilePicker
    ->TemperaturTextFileInterpreter
    ->TemperaturCSVInterpreter
    ->ColumnDataSelector
    ->TemperaturHeaderWriter
    ->BatterietemperaturHeaderWriter
    ->BatteryTableInterpreter
    ->CelsiusToFahrenheitTransformer1
    ->CelsiusToFahrenheitTransformer2
    ->DatabaseLoader
;}

